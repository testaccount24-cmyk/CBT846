TAPECONV TITLE 'Convert file to AWSTAPE format'
*---------------------------------------------------------------------*
* Function:                                                           *
*        This program converts a tape file to AWSTAPE format.         *
*        It reads undefined length blocks of data from SYSUT1 and     *
*        writes each block, prefixed by a 6-byte header, to SYSUT2.   *
*---------------------------------------------------------------------*
TAPECONV CSECT
         LR    R12,R15                  Load base register
         USING TAPECONV,R12             Establish addressability
         OPEN  (SYSUT1,INPUT)           Open input DCB
         TM    SYSUT1+48,X'10'          Is DCB open?
         BZ    EXIT020                  No, exit with RC=20
         OPEN  (SYSUT2,OUTPUT)          Open output DCB
         TM    SYSUT2+48,X'10'          Is DCB open?
         BZ    EXIT020                  No, exit with RC=20
GENLOOP  EQU   *
         GET   SYSUT1                   Get input block
         LR    R2,R1                    R2=>input block
         LH    R4,SYSUT1+82             R4=actual block length
         MVC   HDRPRVLN,HDRCURLN        Copy previous block length
         STCM  R4,B'0001',HDRCURLN      Store low-order length byte
         STCM  R4,B'0010',HDRCURLN+1    Store high-order length byte
         MVI   HDRFLAG1,HDRF1BOR+HDRF1EOR  Set complete record flags
         MVC   SYSUT2+82(2),=H'6'       Set header length in DCB
         PUT   SYSUT2,HEADER            Write block header to SYSUT2
         STH   R4,SYSUT2+82             Set block length in DCB
         PUT   SYSUT2,(R2)              Write data block to SYSUT2
         B     GENLOOP                  Go back for next record
GENEOF   DS    0H
         MVC   HDRPRVLN,HDRCURLN        Copy previous block length
         XC    HDRCURLN,HDRCURLN        Clear current block length
         MVI   HDRFLAG1,HDRF1TMK        Set tape mark flag
         MVC   SYSUT2+82(2),=H'6'       Set header length in DCB
         PUT   SYSUT2,HEADER            Write block header to SYSUT2
         CLOSE (SYSUT1,,SYSUT2)         Close DCBs
         SR    R15,R15                  Zeroize return code
         SVC   3                        Exit with RC=0
EXIT020  DS    0H
         LA    R15,20                   DD statement missing
         SVC   3                        Exit with RC=20
         DROP  R12                      Drop base register
*
* AWSTAPE block header
*
HEADER   DS    0CL6                     Block header
HDRCURLN DC    XL2'0000'                Current block length
HDRPRVLN DC    XL2'0000'                Previous block length
HDRFLAG1 DC    X'00'                    Flags byte 1...
HDRF1BOR EQU   X'80'                    ...beginning of record
HDRF1TMK EQU   X'40'                    ...tape mark
HDRF1EOR EQU   X'20'                    ...end of record
HDRFLAG2 DC    X'00'                    Flags byte 2
*
* Data Control Blocks
*
         PRINT NOGEN
SYSUT1   DCB   DSORG=PS,MACRF=GL,DDNAME=SYSUT1,EODAD=GENEOF,           X
               RECFM=U,LRECL=0,BLKSIZE=32760
SYSUT2   DCB   DSORG=PS,MACRF=PM,DDNAME=SYSUT2,                        X
               RECFM=U,LRECL=0,BLKSIZE=32760
         LTORG
*
* Register equates
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
