TR02ABS  TITLE 'Convert TRK0SAVE IPL Text format to ICKDSF ABSFORMAT'
*---------------------------------------------------------------------*
*                                                                     *
*  Author  :   Sam Golob (most of this was plagiarized from the       *
*                         AWSTAPIN program, from David Cartwright.    *
*                         Thanks, Dave !! )                           *
*                                                                     *
*  Date    :   May 14, 2014                                           *
*                                                                     *
*  Version :   1.0                                                    *
*                                                                     *
*  Changes :                                                          *
*                                                                     *
*  Function:                                                          *
*                                                                     *
*    RECFM=U  input  to  RECFM=U  output.                             *
*                                                                     *
*    This program converts an IPL text file in somitcw's.             *
*    TRK0SAVE format into ICKDSF's ABSFORMAT format............       *
*    Eight-byte CKD records are dropped off each block.........       *
*    But before that was done, if there is a key length and           *
*     a key, it is also dropped.                                      *
*                                                                     *
*    The idea is, that if you only have TRK0SAVE ipltext records,     *
*     you can still convert them and load them with ICKDSF.           *
*                                                                     *
*    ABSFORMAT wants three or more undefined records.                 *
*    The first must be 24 ( X'18' ) bytes.  Decimal 24.               *
*    The second must be 144 ( X'90' ) bytes.  Decimal 144.            *
*    The others are written after the volume label as is,             *
*     starting with record 4.                                         *
*    But ABSFORMAT does not include the count or key fields           *
*     for each record.  And record 3 (the volume id record)           *
*     is not included, for good reason.  You don't want to            *
*     affect the VOLSER of the volume, or the location of             *
*     the VTOC on the volume.                                         *
*                                                                     *
*    The TRK0SAVE program includes count and key fields so:           *
*    The first must be 36 bytes ( C+K+D 8+4+24 ).                     *
*    The second must be 156 bytes ( C+K+D 8+4+144 ).                  *
*    Others are eight bytes larger than data for the                  *
*    count field.  To convert to ABS format, the count                *
*    and key fields in each record must be dropped off.               *
*                                                                     *
*    The count field (always eight bytes) contains the length         *
*     of the key in +5 off the beginning of the count field.          *
*    A halfword +6 off the count field contains the length            *
*     of the actual data in the record.                               *
*                                                                     *
*---------------------------------------------------------------------*
TR02ABS  CSECT
TR02ABS  RMODE 24
TR02ABS  AMODE 24
*---------------------------------------------------------------------*
*    Initialization of the program.                                   *
*---------------------------------------------------------------------*
         LR    R12,R15               Load base register
         USING TR02ABS,R12           Establish addressability
         OPEN  (SYSUT1,INPUT)        Open input DCB
         TM    SYSUT1+48,X'10'       Is DCB open?
         BZ    EXIT020               No, exit with RC=20
         OPEN  (SYSUT2,OUTPUT)       Open output DCB
         TM    SYSUT2+48,X'10'       Is DCB open?
         BZ    EXIT020               No, exit with RC=20
*---------------------------------------------------------------------*
*    Grab each undefined record, to remove the count and key fields.  *
*---------------------------------------------------------------------*
GENLOOP  DS    0H
         GET   SYSUT1                Get input block (this is QSAM GL)
         LR    R2,R1                 R2=>input block (save address)
*---------------------------------------------------------------------*
*                                                                     *
* R2 starts with count CCHHRKLL Cyl, Head(Trk), Rec, Key-len, Data-len*
* R2 displacements are 01234567                                       *
*                                                                     *
*     so that X'5' off the beginning is the one-byte KEY field        *
*     and X'6' for two bytes is the data length field.                *
*                                                                     *
*---------------------------------------------------------------------*
         XR    R5,R5                 Clear
         XR    R6,R6                       work
         XR    R7,R7                            registers
         IC    R5,5(,R2)             Load Key Length
         ICM   R6,B'0011',6(R2)      Load Data length
         LA    R7,8(R5,R6)           Total length of Count-Key-Data
*                                     (not needed for this program)
*
         STH   R6,SYSUT2+82          Set data block length in DCB
         LA    R2,8(R5,R2)           Bump past CK to reach data only
         PUT   SYSUT2,(R2)           Write only data length to SYSUT2
         B     GENLOOP               Go back for next record
GENEOF   DS    0H
         CLOSE (SYSUT1,,SYSUT2)      Close DCBs
         SR    R15,R15               Zeroize return code
         SVC   3                     Exit with RC=0
EXIT020  DS    0H
         LA    R15,20                DD statement missing
         SVC   3                     Exit with RC=20
         DROP  R12                   Drop base register
*
* Data Control Blocks
*
         PRINT GEN
SYSUT1   DCB   DSORG=PS,MACRF=GL,DDNAME=SYSUT1,EODAD=GENEOF,           X
               RECFM=U,LRECL=0,BLKSIZE=32760
SYSUT2   DCB   DSORG=PS,MACRF=PM,DDNAME=SYSUT2,                        X
               RECFM=U,LRECL=0,BLKSIZE=32760
         LTORG
*
* Register equates
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
